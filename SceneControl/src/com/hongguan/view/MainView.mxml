<?xml version="1.0" encoding="utf-8"?>
<spark:ModuleContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
						xmlns:s="library://ns.adobe.com/flex/spark"
						xmlns:mx="library://ns.adobe.com/flex/mx"
						xmlns:tools="library://flex.tools"
						creationComplete="creationCompleteHandler(event)"
						initialize="initializeHandler(event)"
						horizontalCenter="0"
						borderColor="#CCCCCC"
						width="980"
						height="100%" xmlns:spark="org.hu.flextools.components.spark.*">
	
	<fx:Script>
		<![CDATA[
			import com.hongguan.components.NavigatorMenu;
			import com.hongguan.core.ICommunication;
			import com.hongguan.event.LoginEvent;
			import com.hongguan.event.LogoutEvent;
			import com.hongguan.event.NavEvent;
			import com.hongguan.util.FlexUtil;
			import com.hongguan.util.IconStrConst;
			import com.hongguan.util.ViewConst;
			import org.hu.flextools.util.PopUpUtil;
			
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			import mx.events.ModuleEvent;
			
			import spark.components.Application;
			import spark.components.SkinnableContainer;
			
			/**
			 * 初始化此组件的内部结构
			 */
			protected function initializeHandler(event:FlexEvent):void
			{
				FlexUtil.loadModule(ViewConst.LOGIN_URL,this.appModule);
			}
			
			/**
			 * 当组件完成其构建、属性处理、测量、布置和绘制时分派
			 */
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				
			}
			
			/**
			 * 注册当前页面
			 */
			public function applicationClass(app:SkinnableContainer):void
			{ 
				app.addElement(this);
			}

			/**
			 * 登录处理函数
			 */
			protected function loginEvent_Handler(event:LoginEvent):void
			{
				if(event.username == "admin" && event.password == "123")
				{
					FlexUtil.unloadModule(this.appModule);
					FlexUtil.loadModule(ViewConst.SURVEY_URL,this.appModule);
					
					var nav:NavigatorMenu = new NavigatorMenu();
					nav.addEventListener(LogoutEvent.LOGOUT_EVENT,login_Handler);
					nav.addEventListener(NavEvent.NAV_EVENT,menuNav_ClickHandler);

					group.addElementAt(nav,0);
				}
				else
				{
					PopUpUtil.messageBox("输入有误");
				}
			}			
			
			/**
			 * 注销处理函数
			 */
			protected function login_Handler(event:LogoutEvent):void
			{
				var nav:NavigatorMenu = group.getElementAt(0) as NavigatorMenu;
				if(nav)
				{
					group.removeElementAt(0);
				}
				
				FlexUtil.unloadModule(this.appModule);
				FlexUtil.loadModule(ViewConst.LOGIN_URL,this.appModule);
			}
			
			/**
			 * 点击菜单导航处理函数
			 */
			protected function menuNav_ClickHandler(event:NavEvent):void
			{
				if(!event.moduleURL) return;
				
				FlexUtil.unloadModule(this.appModule);
				FlexUtil.loadModule(event.moduleURL,this.appModule);
				
			}
			
			/**
			 * 模块加载进度
			 */
			protected function appModule_progressHandler(e:ModuleEvent):void
			{
				this.progress(Math.round((e.bytesLoaded * 100 / e.bytesTotal)) + "%","100%");
			}
			
			/**
			 * 完成加载模块时分派
			 */
			protected function readyModule(event:ModuleEvent):void
			{
				var ml:ModuleLoader=event.target as ModuleLoader;
				
				var ichild:ICommunication = ml.child as ICommunication;	
				
				this.initModule(ichild);
			}
			
			/**
			 * 初始化模块事件\初始化模块值
			 * 
			 * @param child:DisplayObject 模块子组件
			 */
			private function initModule(ichild:*):void
			{
				if(!ichild) return;
				
				//登录
				if(ichild.SID == ViewConst.LOGIN_URL)
				{
					ichild.addEventListener(LoginEvent.LOGIN_EVENT,loginEvent_Handler);
					(ichild as ICommunication).setMessage(["admin","123"]);
				}
			}		

		]]>
	</fx:Script>	
	
	<s:Scroller width="100%" height="100%">
		<s:Group width="100%" height="100%" id="group">
			<s:layout>
				<s:VerticalLayout gap="0"/>
			</s:layout>			
			<mx:ModuleLoader  id="appModule" horizontalCenter="0" verticalCenter="0" width="100%" height="100%" ready="readyModule(event)" progress="appModule_progressHandler(event)"/>
		</s:Group>
	</s:Scroller>
</spark:ModuleContainer>
