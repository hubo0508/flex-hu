<?xml version="1.0" encoding="utf-8"?>
<!--
<p><b>auth：</b>HUBO</p>
<p><b>create-time：</b>2012-2-9</p>
<p><b>mail：</b>hubo.0508ⓐgmail.com	</p>
-->
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
				   xmlns:s="library://ns.adobe.com/flex/spark"
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   rollOut="rollHandler(event)"
				   rollOver="rollHandler(event)"
				   height="29"
				   borderVisible="true"
				   borderColor="#D9D9D9"
				   minWidth="10"
				   cornerRadius="1">
	
	<fx:Script>
		<![CDATA[
			import com.hubo.workflow.components.graphic.VectorFig;
			import com.hubo.workflow.util.UIUtil;
			
			import mx.managers.PopUpManager;
			
			import spark.components.Group;
			import spark.filters.DropShadowFilter;
			
			private var originalColor:Array = [0xF1F1F1,0xECE9E9];
			private var overColor:Array = [0xF5F5F5,0xEAE9E9];
			
			/**
			 * <p>按钮渐变色的上部分，默认为0xF5F5F5【内部使用】</p>
			 * 
			 * 当鼠标移入、移出时更改此颜色值，从属性originalColora或overColor中取得已经定义的颜色值，当更改后自动生效
			 */
			[Bindable]private var gradientTop:uint =0xF5F5F5;		
			
			/**
			 * <p>按钮渐变色的下部分，默认为0xECE9E9【内部使用】</p>
			 * 
			 * 当鼠标移入、移出时更改此颜色值，从属性originalColora或overColor中取得已经定义的颜色值，当更改后自动生效
			 */
			[Bindable]private var gradientBottom:uint = 0xECE9E9;
			
			/**
			 *  <p>存储颜色投影【内部使用】</p>
			 */
			private var filter:Array = null;
			
			/**
			 * 消息提示容器【内部使用】
			 */
			private var tootip:TooTip;
			
			/**
			 * 显示文本
			 */
			[Bindable]private var _text:String;
			
			/**
			 * <p>指标组件的父级是否为弹出容器(default=false)</p>
			 * 
			 * 此属性对取坐值会有影响
			 */
			private var _isPopMask:Boolean = false;
			
			/**
			 * <p>设定鼠标在移出或移过按钮组件时，是否改变内部组件的颜色值</p>
			 * 
			 * 当内部组件为VectorFig或其属性name=="VectorFig"时，其本身的值为true才会生效
			 */
			private var _triangleOver:Boolean = false;
			
			/**
			 * 继续父容器measure()，重新定义默认高度与宽度
			 */
			override protected function measure():void
			{
				super.measure();
				
				minHeight = 29;
				minWidth = 35;
			}
			
			/**
			 * <p>鼠标移出、移入调用</p>
			 * 
			 * 1、初始化按钮颜色投影。初始化完成后存储到缓存，全局变量中，在第二次使用时，不再创建。</br>
			 * 2、初始化消息提示组件。初始化完成后存储到缓存，全局变量中，在第二次使用时，不再创建。</br>
			 * 3、MouseEvent.ROLL_OUT：将按钮组件投影颜色属性设值为null。隐藏消息提示组件。改边按钮组件的背景渐变颜色。</br>
			 * 3、MouseEvent.ROLL_OVER：将缓存中的投影颜色值赋值到按钮。显示消息提示组件。改边按钮组件的背景渐变颜色。</br>
			 * 4、调用changeTriangleBgColor()，改变内部绘画组成的颜色块。
			 * 
			 * @see com.printingoffice.framework.components.Button.changeTriangleBgColor()
			 */
			protected function rollHandler(event:MouseEvent):void
			{
				if(filter == null)
				{
					filter = [this.createDropShadowFilter()];
				}		
				if(tootip == null)
				{
					var tootipvalue:String = text == null || "" ? this.name : text;
					tootip = new TooTip(tootipvalue);
				}
				
				var bc:BorderContainer=event.currentTarget as BorderContainer;
				switch (event.type)
				{
					case MouseEvent.ROLL_OUT:
						bc.filters=[];
						
						PopUpManager.removePopUp(tootip);
						
						gradientTop = this.originalColor[0];
						gradientBottom = this.originalColor[1];					
						break;
					
					case MouseEvent.ROLL_OVER:
						bc.filters=filter;

						PopUpManager.addPopUp(tootip, UIUtil.getApplication(this), false);
						if(isPopMask)
						{
							tootip.x = (this.x+223) + this.width*0.5 - tootip.width*0.5;
							tootip.y = this.y+102+this.height+tootip.triangleH;	
						}
						else
						{
							var point:Point = UIUtil.getUiAbsolutePosition(this);
							tootip.x = point.x + this.width*0.5 - tootip.width*0.5;
							tootip.y = point.y+this.height+tootip.triangleH;	
						}
						
						gradientTop = this.overColor[0];
						gradientBottom = this.overColor[1];
						break;
					
					default:
						break;
				}
				
				this.changeTriangleBgColor(event.type);
				
			}
			
			/**
			 * <p>设定鼠标在移出或移过按钮组件时，是否改变内部组件的颜色值</p>
			 * 
			 * 当内部组件为VectorFig或其属性name=="VectorFig"时，其本身的值为true才会生效
			 */
			private function changeTriangleBgColor(type:String):void
			{
				if(triangleOver == false)return;
				
				var num:int = this.numElements;
				for(var i:int=0; i<num; i++)
				{
					var obj:Object = this.getElementAt(i);
					if(obj is VectorFig || obj.name=="VectorFig")
					{
						try
						{
							obj.changeBgColor(type);
						}catch(e:*){
							trace(e);
					}
				}						
			}
		}
	
			/**
			 * 创建颜色投影值
			 */
			protected function createDropShadowFilter():spark.filters.DropShadowFilter				
			{		
				var dsf:spark.filters.DropShadowFilter = new spark.filters.DropShadowFilter();
				dsf.angle = 45;
				dsf.blurX = 4;
				dsf.blurY = 4;
				dsf.distance = 0;
				dsf.alpha = 0.3;
				dsf.color = 0x00000;
				dsf.knockout = false;
				dsf.quality = 1;
				dsf.strength = 1;
				dsf.inner = false;
				dsf.hideObject = false;
				
				return dsf;
			}
			
			/**
			 *  显示文本
			 */
			[Bindable(event="text")]
			public function get text():String
			{
				return _text;
			}
			
			/**
			 * @private
			 */
			public function set text(value:String):void
			{
				_text = value;
				
				this.dispatchEvent(new Event("text"));
			}

			/**
			 * <p>指标组件的父级是否为弹出容器(default=false)</p>
			 * 
			 * 此属性对取坐值会有影响
			 */
			public function get isPopMask():Boolean
			{
				return _isPopMask;
			}

			/**
			 * @private
			 */
			public function set isPopMask(value:Boolean):void
			{
				_isPopMask = value;
			}

			/**
			 * <p>设定鼠标在移出或移过按钮组件时，是否改变内部组件的颜色值</p>
			 * 
			 * 当内部组件为VectorFig或其属性name=="VectorFig"时，其本身的值为true才会生效
			 */
			public function get triangleOver():Boolean
			{
				return _triangleOver;
			}

			/**
			 * @private
			 */
			public function set triangleOver(value:Boolean):void
			{
				_triangleOver = value;
			}

			
		]]>
	</fx:Script>
	
	<s:backgroundFill>		
		<s:LinearGradient rotation="90">
			<s:GradientEntry color="{gradientTop}"/>
			<s:GradientEntry color="{gradientBottom}"/>
		</s:LinearGradient>
	</s:backgroundFill>
	
</s:BorderContainer>
