<?xml version="1.0" encoding="utf-8"?>
<!--
	 @作者：HUBO
	 @创建时间：2011-12-5
	 @邮件：hubo.0508@gmail.com
-->
<components1:HContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:components="org.hu.flexeasy.components.*"
					   gap="1"
					   clipAndEnableScrolling="false"
					   paddingLeft="1"
					   paddingBottom="0"
					   paddingRight="0"
					   horizontalCenter="0" xmlns:components1="org.hu.flexeasy.components.*">

	<fx:Metadata>
		[Style(name="headerColor", type="uint", format="Color", inherit="yes", theme="spark")]
		[Style(name="titleBorderColor", type="uint", format="Color", inherit="yes", theme="spark")]
		[Style(name="titleBorderVisible", type="Boolean", inherit="no")]
	</fx:Metadata>

	<fx:Metadata>
		[Event(name="itemClick",type="org.hu.flexeasy.event.GridEvent")]
	</fx:Metadata>

	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.utils.ObjectUtil;
			
			import org.hu.flexeasy.event.GridEvent;
			import org.hu.flexeasy.util.Constant;
			import org.hu.flexeasy.util.UtilFunctions;
			
			import spark.components.BorderContainer;
			import spark.components.CheckBox;

			private var _columns:Array;
			private var _dataProvider:ArrayCollection;
			public var eventMark:String; //click mark
			
			/**
			 * 选中行索引
			 */
			public var indexOfSelectedRow:Array=[];
			
			
			public var selectItems:Array;
			private var _titleRowHeight:int=30;
			private var _rowHeight:int=22;
			private var singleColumnWidth:Number;

			/*default style start*/
			private var rollOverColor:uint=0xB2E1FF;
			private var alternatingItemColors:Array=["#FFFFFF", "#F7F7F7"];
			private var verticalGridLines:Boolean;
			private var selectionColor:uint=0x7FCEFF;
			private var headerColor:uint=0xA8A8A8;

			/*default style end*/
			
			//			<ns:VContainer width="100%" height="60" gap="0">
			//				<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
			//				<ns:HContainer width="100%" height="30" gap="0">
			//					<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
			//					<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
			//				</ns:HContainer>			
			//			</ns:VContainer>
			
			/**
			 * 初始化列数 与 初始化填充行数
			 */
			override protected function createChildren():void
			{
				super.createChildren();

				this.initializationHeaderColumns();
				
				//根据FDataGrid总体高度计算出初始化行数
				var lineHeight:Number = (this.height-titleRowHeight)/rowHeight;
				this.fillBackgroundRows(int(lineHeight) == lineHeight ? lineHeight : Math.floor(lineHeight) + 1);
			}
			
			/**
			 * 初始化填充内容行数
			 * 
			 * <p>
			 * 		请注意：该方法只对架构SDK定义列起作用 【Column、BoxColumn、SequenceColumn】
			 * </p>
			 *
			 * @param rowNum:int 内容行数
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.isSystemColumn(objColumn:Object):Boolean{}
			 */
			protected function fillBackgroundRows(rowNum:int):void
			{
				var len:int=this.columns.length;
				for (var k:int=0; k < rowNum; k++)
				{
					for (var i:int=0; i < len; i++)
					{
						var objColumn:Object=columns[i];
						if (isSystemColumn(objColumn))
						{
							var cell:Cells=getInitializeCell(objColumn, k + 1);
							if ((rowNum - 1) == k)
							{
								this.setLastRowsHeight(k, cell, objColumn);
							}
							objColumn.addElement(cell);
						}
					}
				}
			}

			/**
			 * 设值最后一行行高
			 * 
			 * @param totalRows:int 总行数
			 * @param cell:Cells 目标单元格
			 * @param objColumn:Object 目标列
			 */
			private function setLastRowsHeight(totalRows:int, cell:Cells, objColumn:Object):void
			{
				var lineHeight:Number=this.height - (totalRows * rowHeight + titleRowHeight);
				cell.height=lineHeight;
				if (objColumn is BoxColumn || objColumn is SequenceColumn)
				{
					cell.cellsVisible = false;
				}
			}


			/**
			 * 初始化列数
			 *
			 * <p>调用父级org.hu.flexeasy.ui.datagrid.DataGrid.createChildren()后执行该方法</p>
			 * 
			 * <p>
			 * 		1、该方法将属性columns中的元素取出，添加至显示列表中</br>
			 * 		2、columns中元素可以是架构SDK类型[Column、BoxColumn、SequenceColumn]，也可以是Flex SDK类型</br>
			 * 		3、columns中元素不是架构SDK类型，则不作任何逻辑处理，将对象以最原始的形态添加到显示列表中
			 * </p>
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.isSystemColumn(objColumn:Object):Boolean{}
			 */
			protected function initializationHeaderColumns():void
			{
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					var objColumn:Object=columns[i];
					if (isSystemColumn(objColumn))
					{
						var cells:Cells=this.createCells(objColumn.headerText);
						cells.borderColor=getBorderColor();
						cells.borderSides="";
						cells.backgroupColor=this.getHeaderColor();
						cells.addEventListener(MouseEvent.ROLL_OUT, headerColumnsRollHandler, false, 0, true);
						cells.addEventListener(MouseEvent.ROLL_OVER, headerColumnsRollHandler, false, 0, true);
						cells.addEventListener(MouseEvent.CLICK, headerColumnsClickHandler, false, 0, true);						

						if (objColumn.width == 0)
						{
							cells.percentWidth=100;
							objColumn.percentWidth=100;
						}
						else
						{
							cells.width=objColumn.width;
						}

						cells.height=titleRowHeight;

						if (i == (len - 1))
						{
							objColumn.borderSides="";
						}

						objColumn.addElement(cells);
						objColumn.id=i.toString();
						this.addElement(objColumn as IVisualElement);
					}
					else
					{
						this.addElement(objColumn as IVisualElement);
					}
				}
			}
			
			/**
			 * 鼠标移入、移出标题行单元格
			 */
			protected function headerColumnsRollHandler(event:MouseEvent):void
			{
				this.changeHeaderCellsColor(event.currentTarget as Cells, event.type);
			}
			
			/**
			 * 鼠标点击标题行单元格
			 */
			protected function headerColumnsClickHandler(event:MouseEvent):void
			{
				//do
			}
			
			/**
			 * 设置显示数据源
			 * 
			 * @param value:ArrayCollection 显示数据源
			 */
			public function set dataProvider(value:ArrayCollection):void
			{
				this._dataProvider=value;

				this.dataToColumns();
			}

			/**
			 * 将数据源添加到每列中
			 */
			protected function dataToColumns():void
			{
				var len:int=dataProvider.length;
				for (var i:int=0; i < len; i++)
				{
					this.displayDataToCell(dataProvider.getItemAt(i), i);
				}
			}

			/**
			 * 将数据源添加到所有单元格中
			 * 
			 * <p>
			 * 		列对象中定义有：
			 * 			<ul>
			 * 				<li>org.hu.flexeasy.ui.datagrid.Column.rendererFunction(value:Function)</li>
			 * 				<li>org.hu.flexeasy.ui.datagrid.Column.labelFunction(value:Function)</li>
			 * 			</ul>
			 * 		在创建单元格时，业务逻辑完全由开发人员自己定义
			 * </p>
			 *
			 * @param item:Object 当前行数据源
			 * @param rowIndex:int 当前行索引值(数据源要显示的行索引值)
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.Column.rendererFunction(value:Function){}
			 * @see org.hu.flexeasy.ui.datagrid.Column.labelFunction(value:Function){}
			 */
			public function displayDataToCell(item:Object, rowIndex:int):void
			{
				var len:int=this.columns.length;
				for (var i:int=0; i < len; i++)
				{
					var objColumn:Object=columns[i];
					if (objColumn is Column)
					{
						var column:Column=objColumn as Column;
						var cells:Cells=getHaveDefinedCellsAndSetProperty(column, item, rowIndex+1, column.dataField);
						var fun:Function=column.rendererFunction;
						if (fun != null)
						{
							var bc:IVisualElement=fun.call(null, item, column);
							if (bc.height == 0)
							{
								bc.height=cells.height - 2;
								bc.top=1.5;
							}
							if (bc.width == 0)
							{
								bc.percentWidth=100;
							}
							cells.addElement(bc);
						}
						cells.addEventListener(MouseEvent.ROLL_OUT, cellsRollHandler, false, 0, true);
						cells.addEventListener(MouseEvent.ROLL_OVER, cellsRollHandler, false, 0, true);
						cells.addEventListener(MouseEvent.CLICK, cellsClickHandler, false, 0, true);
					}

					if (objColumn is BoxColumn)
					{
						var boxColumn:BoxColumn=objColumn as BoxColumn;
						var boxCells:Cells=this.getBoxCells(rowIndex+1,boxColumn);
						boxCells.addEventListener(MouseEvent.ROLL_OUT, cellsRollHandler, false, 0, true);
						boxCells.addEventListener(MouseEvent.ROLL_OVER, cellsRollHandler, false, 0, true);
						boxCells.addEventListener(GridEvent.BOX_COLUMN_CHANGE, boxColumnChangeHandler, false, 0, true);
					}
					
					if(objColumn is SequenceColumn)
					{
						var cell:Cells = objColumn.getElementAt(rowIndex+1) as Cells;
						cell.addEventListener(MouseEvent.ROLL_OUT, cellsRollHandler, false, 0, true);
						cell.addEventListener(MouseEvent.ROLL_OVER, cellsRollHandler, false, 0, true);
					}
				}
			}
			
			/**
			 *【 BoxColumn】列状态更改时【true or false】调用
			 * 			 
			 * @see org.hu.flexeasy.event.GridEvent
			 */
			protected function boxColumnChangeHandler(event:GridEvent):void
			{
				var selectIndex:int = int(event.selectRowsIndex[0]);
				var boxColumn:BoxColumn=getBoxColumn();
				if (boxColumn)
				{
					var cells:Cells=boxColumn.getElementAt(selectIndex) as Cells;
					if (cells.selected)
					{
						if (boxColumn.radio)
						{
							this.changeBoxSelected(false, boxColumn);
							this.restoreOriginaColorValueAllRows();
							this.changesInIndex(selectIndex, "newvalue");
							this.changeBoxSelected(true, boxColumn);
							this.changeCellsColor(cells, MouseEvent.CLICK);
						}
						else
						{
							this.changesInIndex(selectIndex, "push");
							this.eventMark=event.type;
							this.changeCellsColor(cells, MouseEvent.CLICK);
						}

						this.selectItems=this.getSelectedValue();
						this.dispatchItemClick();
					}
					else
					{
						this.changesInIndex(event.selectRowsIndex[0], "remove");
						this.restoreOriginaColorValueRows(event.selectRowsIndex[0]);
					}
				}
			}

			/**
			 * 获得已定义的单元格与设置基属性
			 * 
			 * <p>
			 * 		如目标列中定义了【labelFunction】,则单元格文本值由【labelFunction】取得
			 * </p>
			 * 
			 * @param column:Column 目标列
			 * @param item:Object 行数据源
			 * @param rowIndex:int 行索引值
			 * @param dataField:String 
			 * 
			 * @return 获得已定义的单元格
			 */
			protected function getHaveDefinedCellsAndSetProperty(column:Column, item:Object, rowIndex:int, dataField:String):Cells
			{
				var text:String;
				var labelFun:Function=column.labelFunction;
				if (labelFun == null)
				{
					text=item[dataField];
				}
				else
				{
					text=labelFun.call(null, item, column);
				}

				var cells:Cells=column.getElementAt(rowIndex) as Cells;
				cells.data=item;
				cells.text = text;
				
				return cells;
			}
			
			/**
			 * 得到初始化单元格
			 * 
			 * @param objColumn:Object
			 * @param rowIndex:int
			 * @param value:String(default="")
			 * 
			 * @return
			 */
			public function getInitializeCell(objColumn:Object,rowIndex:int, value:String = ""):Cells
			{
				var cells:Cells = this.createCells(value);
				cells.backgroupColor=this.getRowColors(rowIndex);
				cells.percentWidth=100;
				cells.id=rowIndex.toString();
				cells.height=this.rowHeight;
				
				if (objColumn is Column)
				{
					cells.type = Constant.LABEL;
				}

				if(objColumn is BoxColumn)
				{						
					cells.type = Constant.CHECKBOX;
				}
					
				if(objColumn is SequenceColumn)
				{
					cells.text = (rowIndex).toString();
					cells.type = Constant.LABEL;
					
					cells.labelHorizontalCenter = 0;
						
					//this.getTextAlign(objColumn.textAlign.toString());
				}
				
				return cells;
			}

			/**
			 * 根据索引值取得复选框列单元格对象
			 * 
			 * @param rowIndex:int 索引值
			 * @param column:BoxColumn(default=null) 复选框单元格对象
			 * 
			 * @return 指定复选框单元格对象
			 */
			public function getBoxCells(rowIndex:int,column:BoxColumn = null):Cells
			{
				column = column || this.getBoxColumn();
				var cells:Cells=column.getElementAt(rowIndex) as Cells;				
				return cells;
			}


			/**
			 * 1、取得当前行值
			 * 2、设置当前行选择中颜色
			 */
			protected function cellsClickHandler(event:MouseEvent):void
			{
				var cells:Cells=event.currentTarget as Cells;
				var rowIndex:int=int(cells.id);
				if (cells.selectionColorMark)
				{
					this.restoreOriginaColorValueRows(rowIndex);
					this.changeBoxSelectedByIndex(false, rowIndex);
					this.changesInIndex(rowIndex, "remove");
				}
				else
				{
					this.eventMark=event.type;
					var boxColumn:BoxColumn=this.getBoxColumn();					
					if(boxColumn)
					{
						if (!boxColumn.radio && boxColumn.rowMultiSelection)
						{
							this.changesInIndex(rowIndex, "push");
							this.changeBoxSelectedByIndex(true, rowIndex);
						}
						else
						{
							this.changeBoxSelected(false);
							this.restoreOriginaColorValueAllRows();
							this.changesInIndex(rowIndex, "newvalue");
							this.changeBoxSelected(true);
						}
					}
					else
					{
						this.restoreOriginaColorValueAllRows();
						this.changesInIndex(rowIndex, "newvalue");
					}
					this.changeCellsColor(cells, event.type);
					this.selectItems=this.getSelectedValue();
					this.dispatchItemClick();
				}
			}

			public function dispatchItemClick():void
			{
				var e:GridEvent=new GridEvent(GridEvent.ITEM_CLICK);
				e.selectItems=this.selectItems;
				e.selectRowsIndex=indexOfSelectedRow;

				this.dispatchEvent(e);
			}

			/**
			 * 鼠标移入、移出单元格时，更新背景颜色
			 * 
			 * <p>
			 * 		鼠标移入事件类型【MouseEvent.ROLL_OVER、MouseEvent.ROLL_OUT】
			 * </p>
			 */
			protected function cellsRollHandler(event:MouseEvent):void
			{
				switch (event.type)
				{
					case MouseEvent.ROLL_OVER:
						if (eventMark == MouseEvent.CLICK)
						{
							this.eventMark=event.type;
						}
						else
						{
							eventMark="";
						}
						break;

					case MouseEvent.ROLL_OUT:
						if (eventMark == MouseEvent.CLICK)
						{
							eventMark="";
							return;
						}
						break;

					default:
						break;
				}

				this.changeCellsColor(event.currentTarget as Cells, event.type);
			}

			/**
			 * 根据行索引值取得该行值
			 * 
			 * @param rowsIndex:int 某行索引值
			 * 
			 * @return 指定索引行值
			 */
			public function getSingleRowsValue(rowsIndex:int):Object
			{
				var len:int = this.columns.length;
				for(var i:int=0; i<len; i++)
				{
					var column:Column = this.columns[i] as Column;
					if(column)
					{
						var cell:Cells = column.getElementAt(rowsIndex) as Cells;
						return cell.data;
					}
				}
				
				return null;
			}

			/**
			 * 取得选中的值
			 * 
			 * @return 选择值
			 */
			public function getSelectedValue():Array
			{
				var array:Array=[];
				var len:int=indexOfSelectedRow.length;
				var lenColumns:int=columns.length;
				for (var j:int=0; j < len; j++)
				{
					for (var i:int=0; i < lenColumns; i++)
					{
						var column:Column=columns[i] as Column;
						if (column)
						{
							var cells:Cells=column.getElementAt(indexOfSelectedRow[j]) as Cells;
							array.push(cells.data);
							break;
						}
					}
				}

				return array;
			}

			/**
			 * 改变标题行背景颜色
			 * 
			 * @param cells:Cells 目标单元格
			 * @param eventtype:String 事件类型 【MouseEvent.ROLL_OVER、MouseEvent.ROLL_OUT】
			 */
			public function changeHeaderCellsColor(cells:Cells, eventtype:String):void
			{
				var _rollOverColor:uint=this.getRollOverColor();
				var _headerColor:uint=this.getHeaderColor();

				if (MouseEvent.ROLL_OVER == eventtype)
				{
					cells.backgroupColor=_rollOverColor;
					cells.invalidateDisplayList();
				}
				if (MouseEvent.ROLL_OUT == eventtype)
				{
					cells.backgroupColor=_headerColor;
					cells.invalidateDisplayList();
				}
			}

			/**
			 * 更改单元格背景颜色
			 * 
			 * <p>
			 * 		<ul>
			 * 			<li>鼠标点击目标单元格时的背景颜色，从已定义样式文件中取得【selectionColor】</li>
			 * 			<li>鼠标移出目标单元格时的背景颜色，从已定义样式文件中取得【rollOverColor】</li>
			 * 		</ul>
			 * </p>
			 * 
			 * @param cells:Cells 目标单元格
			 * @param eventtype:String 触发毒更改单元格事件类型【MouseEvent.ROLL_OUT,MouseEvent.ROLL_OVER,MouseEvent.CLICK】
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.Cells
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.getSelectionColor():uint{}
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.getRollOverColor():uint{}
			 */
			public function changeCellsColor(cells:Cells, eventtype:String):void
			{
				var _selectionColor:uint=this.getSelectionColor();
				var _rollOverColor:uint=this.getRollOverColor();

				var rowIndex:int=int(cells.id);
				
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					var sibingColumn:Object=columns[i];
					if (isSystemColumn(sibingColumn))
					{						
						var siblingCell:Cells=sibingColumn.getElementAt(rowIndex) as Cells;
						if (MouseEvent.ROLL_OUT == eventtype)
						{
							if (cells.selectionColorMark)
							{
								siblingCell.backgroupColor=_selectionColor;
							}
							else
							{
								siblingCell.backgroupColor=this.getRowColors(rowIndex);
							}
							siblingCell.invalidateDisplayList();
						}
						else if (MouseEvent.ROLL_OVER == eventtype)
						{
							siblingCell.backgroupColor=_rollOverColor;
							siblingCell.invalidateDisplayList();
						}
						else if (MouseEvent.CLICK == eventtype)
						{
							siblingCell.backgroupColor=_selectionColor;
							siblingCell.selectionColorMark=true;
							siblingCell.invalidateDisplayList();
						}
					}
				}
			}

			/**
			 * 将缓存中记录的状态行的颜色值还原到最原始状态(值)
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.indexOfSelectedRow:Array
			 */
			public function restoreOriginaColorValueAllRows():void
			{
				for (var j:int=0; j < indexOfSelectedRow.length; j++)
				{
					this.restoreOriginaColorValueRows(indexOfSelectedRow[j]);
				}
			}

			/**
			 * 根据索引值将某行的颜色值还原到最原始状态(值)
			 * 
			 * <p>
			 * 		该方法只对构架定义列起作用 【Column、BoxColumn、SequenceColumn】
			 * </p>
			 * 
			 * @param rowIndex:int 索引值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.Column
			 * @see org.hu.flexeasy.ui.datagrid.BoxColumn
			 * @see org.hu.flexeasy.ui.datagrid.SequenceColumn
			 */
			public function restoreOriginaColorValueRows(rowIndex:int):void
			{
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					var objColumn:Object=columns[i];
					if (isSystemColumn(objColumn))
					{
						var designateCell:Cells=objColumn.getElementAt(rowIndex) as Cells;
						designateCell.backgroupColor=getRowColors(rowIndex);
						designateCell.selectionColorMark=false;
						designateCell.invalidateDisplayList();
					}
				}
			}
			
			/**
			 * 判断是否为系统定义值
			 * 
			 * @param objColumn:Object 目标列对象
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.Column
			 * @see org.hu.flexeasy.ui.datagrid.BoxColumn
			 * @see org.hu.flexeasy.ui.datagrid.SequenceColumn
			 * 
			 * @return true || false
			 */
			private function isSystemColumn(objColumn:Object):Boolean
			{
				if (objColumn is Column || objColumn is BoxColumn || objColumn is SequenceColumn)
				{
					return true;
				}
				return false;
			}
			
			/**
			 * 将缓存中的选中记录，根据state更改其BoxColumn状态
			 * 
			 * @param state:Boolean 目标状态[true||false]
			 * @param boxColumn:BoxColumn(default=null) 要更改状态的目标列
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.BoxColumn
			 */
			public function changeBoxSelected(state:Boolean, boxColumn:BoxColumn=null):void
			{
				boxColumn = boxColumn || this.getBoxColumn();
				var len:int=indexOfSelectedRow.length;
				for (var j:int=0; j < len; j++)
				{
					var rowIndex:int=indexOfSelectedRow[j];
					this.changeBoxSelectedByIndex(state, rowIndex, boxColumn);
				}
			}

			public function changeBoxSelectedByIndex(state:Boolean, rowIndex:int, boxColumn:BoxColumn=null):void
			{
				boxColumn=boxColumn || this.getBoxColumn();
				var cells:Cells=boxColumn.getElementAt(rowIndex) as Cells;
				if (cells.selected != state)
				{
					(cells.getElementAt(0) as CheckBox).selected=state;
				}
			}
			
			/**
			 * 改变选择行数据存储缓存
			 * 
			 * @param rowIndex:int 索引值
			 * @param type:String(default="push") 类型 【remove、push、newvalue】
			 * @param values:Array 新值
			 */
			private function changesInIndex(rowIndex:int, type:String="push", values:Array=null):void
			{
				if (type == "remove")
				{
					var len:int=this.indexOfSelectedRow.length;
					for (var i:int=0; i < len; i++)
					{
						var oldRowIndex:int=int(this.indexOfSelectedRow[i]);
						if (oldRowIndex == rowIndex)
						{
							this.indexOfSelectedRow.splice(i, 1);
							break;
						}
					}
					return;
				}
				if (type == "push")
				{
					this.indexOfSelectedRow.push(rowIndex);
					return;
				}
				if (type == "newvalue")
				{
					if (values)
					{
						this.indexOfSelectedRow=values;
					}
					else
					{
						this.indexOfSelectedRow=[rowIndex];
					}
					return;
				}
			}
			
			/**
			 * 取得已经定义列BoxColumn
			 * 
			 * @return  org.hu.flexeasy.ui.datagrid.BoxColumn
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.BoxColumn
			 */
			public function getBoxColumn():BoxColumn
			{
				var boxColumn:BoxColumn;
				var lenColumns:int=columns.length;
				for (var i:int=0; i < lenColumns; i++)
				{
					boxColumn=columns[i] as BoxColumn;
					if (boxColumn)
					{
						break;
					}
				}
				return boxColumn;
			}

			private function getTextAlign(value:String = null):Object
			{
				var _textAlign:String= value == null ? getStyle("textAlign") : value ;
				if (_textAlign == "start" || _textAlign == "center")
				{
					return "0";
				}
				else
				{
					_textAlign;
				}

				return _textAlign;
			}

			/**
			 * 取得定义某单元格选中的颜色值
			 * 
			 * <p>
			 * 		某单元格选中的颜色属性【selectionColor】，系统默认值为【0x7FCEFF】，如需重新定义请设定其样式；
			 * </p>
			 * 
			 * @return 具体颜色值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.selectionColor:uint
			 */
			protected function getSelectionColor():uint
			{
				var _selectionColor:uint=getStyle("selectionColor");
				_selectionColor=_selectionColor == 0 ? selectionColor : _selectionColor;
				return _selectionColor;
			}

			/**
			 * 取得定义某单元格鼠标移入的颜色值
			 * 
			 * <p>
			 * 		某单元格鼠标移入的颜色属性【rollOverColor】，系统默认值为【0xB2E1FF】，如需重新定义请设定其样式；
			 * </p>
			 * 
			 * @return 具体颜色值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.rollOverColor:uint
			 */
			protected function getRollOverColor():uint
			{
				var _rollOverColor:uint=getStyle("rollOverColor");
				_rollOverColor=_rollOverColor == 0 ? rollOverColor : _rollOverColor;
				return _rollOverColor;
			}

			/**
			 * 取得定义某标题头单元格的颜色值
			 * 
			 * <p>
			 * 		某标题头单元格的颜色属性【rollOverColor】，系统默认值为【0xA8A8A8】，如需重新定义请设定其样式；
			 * </p>
			 * 
			 * @return 具体颜色值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.headerColor:uint
			 */
			protected function getHeaderColor():uint
			{
				var _headerColor:uint=getStyle("headerColor");
				_headerColor=_headerColor == 0 ? headerColor : _headerColor;
				return _headerColor;
			}

			/**
			 * 取得定义某标题头单元格的颜色值
			 * 
			 * <p>
			 * 		某标题头单元格的颜色属性【borderColor】，系统默认值为【borderColor】，如需重新定义请设定其样式；
			 * </p>
			 * 
			 * @return 具体颜色值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.borderColor:uint
			 */
			protected function getBorderColor():uint
			{
				var _borderColor:uint=this.getStyle("borderColor");
				_borderColor == 0 ? borderColor : _borderColor;
				return _borderColor;
			}

			/**
			 * 根据索引值取得行单元格对象
			 * 
			 * @param rowsIndex:int 行索引值
			 * 
			 * @return 行单元格
			 */
			public function getRowCellsByIndex(rowsIndex:int):Array
			{
				var rowCells:Array=[];
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					var column:Column=columns[i] as Column;
					if (column)
					{
						rowCells.push(column.getElementAt(rowsIndex));
					}
				}
				return rowCells;
			}

			/**
			 * 根据行索引取得行颜色值
			 * 
			 * <p>
			 * 		行颜色属性【alternatingItemColors】，系统默认值为["#FFFFFF", "#F7F7F7"]，如需重新定义请设定其样式；
			 * </p>
			 * 
			 * @param rowIndex:int 行索引值
			 * 
			 * @return 具体颜色值
			 * 
			 * @see org.hu.flexeasy.ui.datagrid.DataGrid.alternatingItemColors:Array
			 */
			protected function getRowColors(rowIndex:int):uint
			{
				var _alternatingItemColors:Array=this.getStyle("alternatingItemColors");
				_alternatingItemColors=_alternatingItemColors.length == 0 ? alternatingItemColors : _alternatingItemColors;

				if (rowIndex >= 0 && rowIndex <= 9)
				{
					return getItemColorsByIndex(_alternatingItemColors, rowIndex);
				}
				else
				{
					var rowIndexStr:String=rowIndex.toString();
					rowIndex=int(rowIndexStr.substr(rowIndexStr.length - 1));
					return getItemColorsByIndex(_alternatingItemColors, rowIndex);
				}
			}

			/**
			 * 根据索引值得到奇、偶数颜色值
			 * 
			 * @param itemColors:Array 奇、偶数颜色值
			 * @param index:int 奇数或偶数【0、1、2、3、4、5、6、7、8、9】
			 * 
			 * @return 颜色值
			 */
			public function getItemColorsByIndex(itemColors:Array, index:int):uint
			{
				if (index == 0 || index == 2 || index == 4 || index == 6 || index == 8)
				{
					return itemColors[0] as uint;
				}
				else
				{
					return itemColors[1] as uint;
				}
			}

			/**
			 * 创建单元
			 */
			public function createCells(text:String):Cells
			{
				var column:Cells=new Cells();
				column.text=text;

				var textAlign:String=this.getTextAlign().toString();
				if (textAlign == "left")
				{
					column.labelHorizontalCenter="undefined";
					column.labelLeft=2;
				}

				if (textAlign == "right")
				{
					column.labelHorizontalCenter="undefined";
					column.labelRight=2;
				}

				return column;
			}

			public function set columns(value:Array):void
			{
				_columns=value;
			}

			public function get dataProvider():ArrayCollection
			{
				return _dataProvider;
			}

			public function get columns():Array
			{
				return _columns;
			}

			public function get titleRowHeight():int
			{
				return _titleRowHeight;
			}

			public function set titleRowHeight(value:int):void
			{
				_titleRowHeight=value;
			}

			public function get rowHeight():int
			{
				return _rowHeight;
			}

			public function set rowHeight(value:int):void
			{
				_rowHeight=value;
			}
			
			private function setColumnsWidth():void
			{
				var singleColumnWidth:Number=getSingleColumnWidth();
				var len:int=this.columns.length;
				for (var i:int=0; i < len; i++)
				{
					var obj:Object=columns[i];
					if (obj.width == obj.measuredWidth)
					{
						obj.width=singleColumnWidth;
					}
				}
			}
			
			private function getSingleColumnWidth():Number
			{
				var manuallyDefineColumnWidth:Number=0;
				var count:int=0;
				
				var len:int=this.columns.length;
				for (var i:int=0; i < len; i++)
				{
					var obj:Object=columns[i];
					if (obj.width == 0)
					{
						manuallyDefineColumnWidth+=Number(obj.width);
						count++;
					}
				}
				
				return (this.width - manuallyDefineColumnWidth) / (len - count);
			}
		]]>
	</fx:Script>

</components1:HContainer>
