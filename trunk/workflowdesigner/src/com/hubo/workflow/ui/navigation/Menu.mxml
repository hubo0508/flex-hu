<?xml version="1.0" encoding="utf-8"?>
<!--
	 <p><b>auth：</b>HUBO</p>
	 <p><b>create-time：</b>2012-7-1</p>
	 <p><b>mail：</b>hubo.0508ⓐgmail.com	</p>
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:components="com.hubo.workflow.components.*"
		 width="100%"
		 top="88">
	
	<fx:Metadata>
		[Event(name="rollMenu",type="mx.events.DynamicEvent")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.DynamicEvent;
			
			override protected function createChildren():void
			{
				super.createChildren();
				
				this.initEvent();
			}
			
			private function initEvent():void
			{
				for(var i:int=0, num:int=hgroup.numElements; i<num; i++)
				{
					var mButton:IVisualElement = hgroup.getElementAt(i);
					if(mButton is MenuButton)
					{
						mButton.addEventListener(MouseEvent.ROLL_OVER, mButtonRollHandler,false,0,true);
						mButton.addEventListener(MouseEvent.ROLL_OUT, mButtonRollHandler,false,0,true);
					}
				}
			}
			
			protected function  mButtonRollHandler(event:MouseEvent):void
			{
				var mButton:MenuButton = event.currentTarget as MenuButton;
				
				switch(event.type)
				{
					case MouseEvent.ROLL_OUT :
						this.dispatchMenuEvent(MouseEvent.ROLL_OUT, mButton.isBgEvent, mButton.text);
						break;
					
					case MouseEvent.ROLL_OVER :
						this.dispatchMenuEvent(MouseEvent.ROLL_OVER, mButton.isBgEvent, mButton.text);
						break;
					
					default:
						break;
				}
			}

			private function dispatchMenuEvent(type:String, isBgEvent:Boolean, text:String):void
			{
				if(!isBgEvent)return;
				
				var dye:DynamicEvent = new DynamicEvent("rollMenu");
				dye.rolltype = type;
				dye.text = text;
				
				this.dispatchEvent(dye);
			}
			
			public function hasShownToMenu(text:String, mark:Boolean):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.hasShownToMenu = mark;
			}
			
			/**
			 * 添加菜单对象(MenuButton)样式
			 */
			public function addedBgStyle(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.addedBgStyle();
			}
			
			public function stopMenuTimer(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.addedBgStyle();
			}
			
			/**
			 * 删除菜单对象(MenuButton)样式
			 */
			public function removeBgStyle(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.removeBgStyle();
			}
			
			/**
			 * 根据文本取得菜单对象(MenuButton)
			 */
			public function getMenuButtonByText(text:String):MenuButton
			{
				var returnvalue:MenuButton;
				for(var i:int=0, num:int=hgroup.numElements; i<num; i++)
				{
					if(hgroup.getElementAt(i) is MenuButton)
					{
						var mButton:MenuButton = hgroup.getElementAt(i) as MenuButton;
						if(mButton.text == text)
						{
							returnvalue = mButton;
							break;
						}
					}
				}
				
				return returnvalue;
			}
		]]>
	</fx:Script>
	
	<s:HGroup width="100%" gap="0" id="hgroup">
		<components:MenuButton text="请假审批流程" texttype="title" width="225" isBgEvent="false"/>
		<components:Separator/>
		<components:MenuButton text="节点" textcolor="0xDA2323" isDropDownMenu="true"/>
		<components:Separator/>
		<components:MenuButton text="立即保存"/>
		<components:Separator/>
		<components:MenuButton text="舍弃"/>
		<components:Separator/>
		<components:MenuButton text="验证"/>
		<components:Separator/>
		<components:MenuButton text="对齐" isDropDownMenu="true"/>
		<components:Separator/>
		<components:MenuButton text="缩放" isDropDownMenu="true"/>
		<components:Separator/>
		<components:MenuButton text="网格"/>
		<components:Separator/>
		<components:MenuButton text="日志"/>
		<components:Separator/>
		<components:MenuButton width="100%" isBgEvent="false"/>
	</s:HGroup>
	
</s:Group>
