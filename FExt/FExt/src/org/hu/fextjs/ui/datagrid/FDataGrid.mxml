<?xml version="1.0" encoding="utf-8"?>
<!--
	 @作者：HUBO
	 @创建时间：2011-12-5
	 @邮件：hubo.0508@gmail.com
-->
<components:HContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:components="org.hu.fextjs.components.*"
					   gap="1"
					   paddingLeft="0"
					   horizontalCenter="0">

	<fx:Metadata>
		[Style(name="titleBackgroupColor", type="uint", format="Color", inherit="yes", theme="spark")]
		[Style(name="titleBorderColor", type="uint", format="Color", inherit="yes", theme="spark")]
		[Style(name="titleBorderVisible", type="Boolean", inherit="no")]
	</fx:Metadata>
	
	<fx:Metadata>
		[Event(name="itemClick",type="org.hu.fextjs.event.GridEvent")]
	</fx:Metadata>

	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.IVisualElement;
			
			import org.hu.fextjs.event.GridEvent;
			
			import spark.components.CheckBox;

			private var _columns:Array;
			private var _mergeCells:Array;
			private var _dataProvider:Object;
			public var eventMark:String;//click mark
			public var historySelectRowsIndex:Array=[];
			public var selectItems:Array;
			private var _titleRowHeight:int = 0;
			private var _associationsChoose:Boolean = true;

			/*default style start*/
			private var rollOverColor:uint=0xB2E1FF;
			private var alternatingItemColors:Array=["#FFFFFF", "#F7F7F7"];
			private var verticalGridLines:Boolean;
			private var titleBorderVisible:Boolean=true;
			private var selectionColor:uint=0x7FCEFF;
			private var titleBackgroupColor:uint = 0xA8A8A8;
			/*default style end*/	

			override protected function createChildren():void
			{
				super.createChildren();
//				trace("+++++++++++++FDataGrid++++createChildren");
//				trace(this.getStyle("titleBackgroupColor"));
				this.displayTitleRow();
			}
			
//			<ns:VContainer width="100%" height="60" gap="0">
//				<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
//				<ns:HContainer width="100%" height="30" gap="0">
//					<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
//					<ns:VContainer backgroupColor="#919191" width="100%" height="30"/>
//				</ns:HContainer>			
//			</ns:VContainer>

			/**
			 * 初始化单元格列数
			 */
			protected function displayTitleRow():void
			{
				var len:int=_columns.length;
				for (var i:int=0; i < len; i++)
				{					
					var objColumn:Object = _columns[i];
					if (objColumn is FColumn || objColumn is FBoxColumn)
					{
						var cells:FCells=this.createCells(objColumn.headerText);
						this.initTitleRowEventListener(cells);
						this.settingTitleCellsStyle(cells);
						
						if (objColumn.width == 0)
						{
							cells.percentWidth=100;
							objColumn.percentWidth=100;
						}
						else
						{
							cells.width = objColumn.width;
						}
						
						if(titleRowHeight != 0)
						{
							cells.height = titleRowHeight;
						}
						
						objColumn.addElement(cells);
						objColumn.id=i + "";
						this.addElement(objColumn as IVisualElement);
					}
					else
					{
						this.addElement(objColumn as IVisualElement);
					}
				}
			}
			
			public function initTitleRowEventListener(cells:FCells):void
			{
				cells.addEventListener(MouseEvent.ROLL_OUT,titleRowRollHandler,false,0,true);
				cells.addEventListener(MouseEvent.ROLL_OVER,titleRowRollHandler,false,0,true);
				cells.addEventListener(MouseEvent.CLICK,titleRowClickHandler,false,0,true);
			}
			
			protected function titleRowRollHandler(event:MouseEvent):void
			{
				this.changeTitleCellsColor(event.currentTarget as FCells,event.type);
			}
			
			protected function titleRowClickHandler(event:MouseEvent):void
			{
				//do
			}
			
			/**
			 * 设置列表头样式
			 */
			private function settingTitleCellsStyle(cells:FCells):void
			{
				var _borderColor:uint=this.getStyle("borderColor");
				cells.borderColor=_borderColor == 0 ? borderColor : _borderColor;

				var _titleBorderVisible:Boolean=this.getStyle("titleBorderVisible");
				if (_titleBorderVisible)
				{
					cells.borderSides="bottom";
				}
				else
				{
					cells.borderSides="";
				}

				var _titleBackgroupColor:uint=this.getStyle("titleBackgroupColor");
				cells.backgroupColor=_titleBackgroupColor == 0 ? backgroupColor : _titleBackgroupColor;
			}

			public function set dataProvider(value:Object):void
			{
				this._dataProvider=value;

				this.eachDataProvider();
			}

			/**
			 * 创建并显示数据
			 */
			protected function eachDataProvider():void
			{
				if (_dataProvider is ArrayCollection)
				{
					var ac:ArrayCollection=_dataProvider as ArrayCollection;
					var len:int=ac.length;
					for (var i:int=0; i < len; i++)
					{
						this.settingRowData(ac.getItemAt(i), i);
					}
				}
			}

			/**
			 * 根据行数据创建行单元格数据
			 *
			 * item:当前行数据
			 * rowIndex:当前行号
			 */
			protected function settingRowData(item:Object, rowIndex:int):void
			{
				var len:int=this.columns.length;
				for (var i:int=0; i < len; i++)
				{
					var objColumn:Object = _columns[i];
					if (objColumn is FColumn)
					{
						var column:FColumn=this.columns[i] as FColumn;
						if (column)
						{
							var cells:FCells=getFCells(column, item, rowIndex, column.dataField);
							this.initContentRowEventListener(cells);
							column.addElement(cells);
						}
					}
					
					if(objColumn is FBoxColumn)
					{
						var boxColumn:FBoxColumn = this.columns[i] as FBoxColumn;
						if(boxColumn)
						{
							var boxCells:FCells = this.getBoxCells(boxColumn,rowIndex);
							boxCells.addEventListener(MouseEvent.ROLL_OUT, cellsRollHandler, false, 0, true);
							boxCells.addEventListener(MouseEvent.ROLL_OVER, cellsRollHandler, false, 0, true);
							boxCells.addEventListener(GridEvent.BOX_COLUMN_CHANGE,boxColumnChangeHandler,false,0,true);
							boxColumn.addElement(boxCells);
						}						
					}
				}
			}
			
			protected function boxColumnChangeHandler(event:GridEvent):void
			{				
				var len:int = this.columns.length;
				for(var i:int=0; i<len; i++)
				{
					var boxColumn:FBoxColumn = this.columns[i] as FBoxColumn;
					if(boxColumn)
					{
						var cells:FCells = boxColumn.getElementAt(int(event.selectRowsIndex[0])) as FCells;						
						if(cells.selected)
						{						
							this.changeRowsColor(cells,MouseEvent.CLICK);						
						}
						else
						{
							//单选
							if(boxColumn.radio){
								this.clearOtherOptionsBoxState(boxColumn,false);
								historySelectRowsIndex.push(event.selectRowsIndex[0]);
								this.clearOtherOptionsRowColors();
							}else{
								
							}							
						}
						
						break;
					}					
				}				
			}
			
			public function initContentRowEventListener(cells:FCells):void				
			{
				cells.addEventListener(MouseEvent.ROLL_OUT, cellsRollHandler, false, 0, true);
				cells.addEventListener(MouseEvent.ROLL_OVER, cellsRollHandler, false, 0, true);
				cells.addEventListener(MouseEvent.CLICK, cellsClickHandler, false, 0, true);
			}

			/**
			 * 创建统一样式单元格
			 */
			public function getFCells(column:FColumn, item:Object, rowIndex:int, dataField:String):FCells
			{
				var cells:FCells=this.createCells(item[dataField]);
				cells.backgroupColor=this.getRowColors(rowIndex);
				cells.borderSides="";
				if (column.percentWidth == 100)
				{
					cells.percentWidth=100;
				}
				else
				{
					cells.width=column.width;
				}
				cells.id=rowIndex + "";

				return cells;
			}
			
			public function getBoxCells(column:FBoxColumn,rowIndex:int):FCells
			{
				var cells:FCells = new FCells();
				cells.type = column.boxType;
				cells.backgroupColor=this.getRowColors(rowIndex);
				cells.borderSides="";
				if (column.percentWidth == 100)
				{
					cells.percentWidth=100;
				}
				else
				{
					cells.width=column.width;
				}
				cells.id=rowIndex + "";
				
				return cells;
			}


			/**
			 * 1、取得当前行值
			 * 2、设置当前行选择中颜色
			 */
			protected function cellsClickHandler(event:MouseEvent):void
			{
				this.eventMark=event.type;
				this.changeRowsColor(event.currentTarget as FCells, event.type);
				this.selectItems = this.getSelectRowValue();
				this.setCascadeState(true);
				this.dispatchItemClick();
			}

			public function dispatchItemClick():void
			{
				var e:GridEvent = new GridEvent(GridEvent.ITEM_CLICK);
				e.selectItems = this.selectItems;
				e.selectRowsIndex = historySelectRowsIndex;
				
				this.dispatchEvent(e);
			}
			
			/**
			 * 设置级联选择
			 */
			protected function setCascadeState(selected:Boolean,rowsIndex:String = null):void
			{
				var len:int = historySelectRowsIndex.length;
				var lenColumns:int=columns.length;
				for (var j:int=0; j < len; j++)
				{
					for (var i:int=0; i < lenColumns; i++)
					{
						var boxColumn:FBoxColumn=columns[i] as FBoxColumn;
						if (boxColumn)
						{
							var cells:FCells = boxColumn.getElementAt(historySelectRowsIndex[j]) as FCells;
							if(cells.id != rowsIndex)
							{
								(cells.getElementAt(0) as CheckBox).selected = selected;
							}							
						}
					}
				}
			}
			
			/**
			 * 1、鼠标移入，移出单元格时，更新背景颜色
			 * 2、同时更新当前行的颜色
			 */
			protected function cellsRollHandler(event:MouseEvent):void
			{
				switch(event.type)
				{
					case MouseEvent.ROLL_OVER:
						if (eventMark == MouseEvent.CLICK)
						{
							this.eventMark=event.type;
						}
						else
						{
							eventMark="";
						}
						break;
					
					case MouseEvent.ROLL_OUT:
						if (eventMark == MouseEvent.CLICK)
						{
							eventMark="";
							return;
						}
						break;
					
					default:
						break;
				}
				
				this.changeRowsColor(event.currentTarget as FCells, event.type);				
			}
			
			/**
			 * 根据行索引取得单元格值
			 */
			public function getColumnValueByRowIndex(rowIndex:Array,column:FColumn,target:Array = null):Array
			{
				var array:Array = [];
				
				var len:int = rowIndex.length;
				for(var i:int=0; i<len; i++)
				{
					var cells:FCells = column.getElementAt(rowIndex[i]) as FCells;
					if(target && target[i])
					{
						var obj:Object = target[i];
						obj[column.dataField] = cells.text;
					}
					else
					{
						array.push(column.dataField,cells.text);
					}					
				}
				
				return target == null ? array : target;
			}
			
			/**
			 * 取得所有组件自定义值
			 */
			public function getSelectRowValue():Array
			{
				var array:Array = [];
				var len:int = historySelectRowsIndex.length;
				var lenColumns:int=columns.length;
				for (var j:int=0; j < len; j++)
				{
					var obj:Object = new Object();
					for (var i:int=0; i < lenColumns; i++)
					{
						var column:FColumn=columns[i] as FColumn;
						if (column)
						{
							var cells:FCells = column.getElementAt(historySelectRowsIndex[j]) as FCells;
							obj[column.dataField] = cells.text;							
						}
					}
					array.push(obj);
				}
				
				return array;
			}
			
			/**
			 * 改变标题呢行背景颜色
			 */
			public function changeTitleCellsColor(cells:FCells, eventtype:String):void
			{				
				
				var _rollOverColor:uint=getStyle("rollOverColor");
				_rollOverColor=_rollOverColor == 0 ? rollOverColor : _rollOverColor;
				
				var _titleBackgroupColor:uint = getStyle("titleBackgroupColor");
				_titleBackgroupColor=_titleBackgroupColor == 0 ? titleBackgroupColor : _titleBackgroupColor;
				
				if (MouseEvent.ROLL_OVER == eventtype)
				{
					cells.backgroupColor=_rollOverColor;
					cells.invalidateDisplayList();
				}
				else if (MouseEvent.ROLL_OUT == eventtype)
				{
					cells.backgroupColor=_titleBackgroupColor;
					cells.invalidateDisplayList();
				}
			}

			/**
			 * 改变内容行背景颜色
			 */
			public function changeRowsColor(cells:FCells, eventtype:String):void
			{
				if (MouseEvent.CLICK == eventtype)
				{					
					this.setCascadeState(false,cells.id);
					this.clearOtherOptionsRowColors();					
				}

				var _selectionColor:uint=getStyle("selectionColor");
				_selectionColor=_selectionColor == 0 ? selectionColor : _selectionColor;

				var _rollOverColor:uint=getStyle("rollOverColor");
				_rollOverColor=_rollOverColor == 0 ? rollOverColor : _rollOverColor;
	
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					//var sibingColumn:FColumn=columns[i] as FColumn;
					var sibingColumn:Object=columns[i];
					if (sibingColumn is FColumn || sibingColumn is FBoxColumn)
					{
						var rowIndex:int=int(cells.id) + 1;
						var siblingCell:FCells=sibingColumn.getElementAt(rowIndex) as FCells;
						if (MouseEvent.ROLL_OUT == eventtype)
						{
							if (cells.selectionColorMark)
							{
								siblingCell.backgroupColor=_selectionColor;
							}
							else
							{
								siblingCell.backgroupColor=this.getRowColors(rowIndex - 1);
							}
							siblingCell.invalidateDisplayList();
						}
						else if (MouseEvent.ROLL_OVER == eventtype)
						{
							siblingCell.backgroupColor=_rollOverColor;
							siblingCell.invalidateDisplayList();
						}
						else if (MouseEvent.CLICK == eventtype)
						{
							siblingCell.backgroupColor=_selectionColor;
							siblingCell.selectionColorMark=true;
							historySelectRowsIndex=[rowIndex];
							siblingCell.invalidateDisplayList();
						}
					}
				}
			}

			/**
			 * 清除其它选择行的颜色
			 */
			private function clearOtherOptionsRowColors():void
			{
				for (var j:int=0; j < historySelectRowsIndex.length; j++)
				{
					var len:int=columns.length;
					for (var i:int=0; i < len; i++)
					{
						//var column:FColumn=columns[i] as FColumn;
						var objColumn:Object=columns[i];
						if (objColumn is FColumn || objColumn is FBoxColumn )
						{
							var rowIndex:int=historySelectRowsIndex[j];
							var designateCell:FCells=objColumn.getElementAt(historySelectRowsIndex[j]) as FCells;
							designateCell.backgroupColor=getRowColors(rowIndex+1);
							designateCell.selectionColorMark=false;
							designateCell.invalidateDisplayList();
						}
					}
				}
			}
			
			private function clearOtherOptionsBoxState(boxColumn:FBoxColumn,state:Boolean):void
			{
				var lenRec:int = this.historySelectRowsIndex.length;
				var len:int = boxColumn.numElements;
				for(var j:int = 0; j<lenRec; j++)
				{
					for(var i:int = 0; i<len; i++)
					{
						var designateCell:FCells = boxColumn.getElementAt(historySelectRowsIndex[j]) as FCells;
						designateCell.selected = state;
					}
				}				
			}
			
			/**
			 * 根据列表行索引取得该行所有行单元格
			 */
			private function getRowCellsByRowIndex(rowsIndex:int):Array
			{
				var rowCells:Array = [];
				var len:int=columns.length;
				for (var i:int=0; i < len; i++)
				{
					var column:FColumn=columns[i] as FColumn;
					if (column)
					{
						rowCells.push(column.getElementAt(rowsIndex));
					}
				}
				return rowCells;
			}
			
			/**
			 * 根据列表行索引取行颜色值
			 */
			public function getRowColors(rowIndex:int):uint
			{
				var _alternatingItemColors:Array=this.getStyle("alternatingItemColors");
				_alternatingItemColors=_alternatingItemColors.length == 0 ? alternatingItemColors : _alternatingItemColors;

				if (rowIndex >= 0 && rowIndex <= 9)
				{
					return getItemColorsByIndex(_alternatingItemColors, rowIndex);
				}
				else
				{
					var rowIndexStr:String=rowIndex.toString();
					rowIndex=int(rowIndexStr.substr(rowIndexStr.length - 1));
					return getItemColorsByIndex(_alternatingItemColors, rowIndex);
				}
			}
			
			/**
			 * 根据行索引得到奇、偶数颜色值
			 */
			private function getItemColorsByIndex(_alternatingItemColors:Array, index:int):uint
			{
				if (index == 0 || index == 2 || index == 4 || index == 6 || index == 8)
				{
					return _alternatingItemColors[0] as uint;
				}
				else
				{
					return _alternatingItemColors[1] as uint;
				}
			}

			/**
			 * 创建单元
			 */
			private function createCells(text:String):FCells
			{
				var column:FCells=new FCells();
				column.text=text;

				return column;
			}
	

			public function set columns(value:Array):void
			{
				_columns=value;
			}

			public function get dataProvider():Object
			{
				return _dataProvider;
			}

			public function get columns():Array
			{
				return _columns;
			}

			public function get titleRowHeight():int
			{
				return _titleRowHeight;
			}

			public function set titleRowHeight(value:int):void
			{
				trace("_titleRowHeight");
				_titleRowHeight = value;
			}

			public function get mergeCells():Array
			{
				return _mergeCells;
			}

			public function set mergeCells(value:Array):void
			{
				_mergeCells = value;
			}

			public function get associationsChoose():Boolean
			{
				return _associationsChoose;
			}

			public function set associationsChoose(value:Boolean):void
			{
				_associationsChoose = value;
			}

			
		]]>
	</fx:Script>
	
</components:HContainer>
