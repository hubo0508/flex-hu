<?xml version="1.0" encoding="utf-8"?>
<!--
	 <p><b>auth：</b>HUBO</p>
	 <p><b>create-time：</b>2012-7-1</p>
	 <p><b>mail：</b>hubo.0508ⓐgmail.com	</p>
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:components="com.hubo.workflow.components.*"
		 width="100%"
		 top="88">
	
	<!-- 
	菜单对象容器
	-->
	
	<fx:Metadata>
		[Event(name="rollMenuLevel",type="mx.events.DynamicEvent")]
		[Event(name="clickMenuLevel",type="mx.events.DynamicEvent")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.DynamicEvent;

			public static const ROLL_MENU_LEVEL:String = "rollMenuLevel";
			public static const CLICK_MENU_LEVEL:String = "clickMenuLevel";
			
			public static const LOG:String = "log";//日志
			public static const GRID:String = "grid";//网格
			public static const ZOOM:String = "zoom";//缩放
			public static const ALIGNMENT:String = "alignment";//对齐
			public static const VERIFICATION:String = "verification";//验证
			public static const CANCEL:String = "cancel";//舍弃
			public static const SAVE:String = "save";//立即保存
			public static const NODE:String = "node";//节点
			
			/**
			 * 初始化创建对象
			 */
			override protected function createChildren():void
			{
				super.createChildren();
				
				this.initEvent();
			}
			
			/**
			 * 初始事件监听
			 */
			private function initEvent():void
			{
				for(var i:int=0, num:int=hgroup.numElements; i<num; i++)
				{
					var mButton:MenuButton = hgroup.getElementAt(i) as MenuButton;
					if(mButton && mButton.isBgEvent)
					{
						mButton.addEventListener(MouseEvent.ROLL_OVER, mButtonRollHandler,false,0,true);
						mButton.addEventListener(MouseEvent.ROLL_OUT, mButtonRollHandler,false,0,true);
						mButton.addEventListener(MouseEvent.CLICK,mButtonClickHandler,false,0,true);
					}
				}
			}
			
			protected function mButtonClickHandler(event:MouseEvent):void
			{
				var mButton:MenuButton = event.currentTarget as MenuButton;
				
				var dye:DynamicEvent = new DynamicEvent(CLICK_MENU_LEVEL);
				dye.text = mButton.text;
				dye.menutype = mButton.menutype;
				
				this.dispatchEvent(dye);
			}
			
			/**
			 * 一级菜单鼠标事件,MouseEvent.ROLL_OUT与MouseEvent.ROLL_OVER
			 */
			protected function  mButtonRollHandler(event:MouseEvent):void
			{
				var mButton:MenuButton = event.currentTarget as MenuButton;
				
				switch(event.type)
				{
					case MouseEvent.ROLL_OUT :
						if(mButton.triangleOver && mButton.isDropDownMenu)
						{
							mButton.setTriangleRolloutColor(mButton.triangleOriginalColor);
						}
						this.dispatchMenuEvent(MouseEvent.ROLL_OUT, mButton);
						break;
					
					case MouseEvent.ROLL_OVER :
						if(mButton.triangleOver && mButton.isDropDownMenu)
						{
							mButton.setTriangleRolloverColor(mButton.triangleOverColor);
						}
						this.dispatchMenuEvent(MouseEvent.ROLL_OVER, mButton);
						break;
					
					default:
						break;
				}
			}
			
			private function dispatchMenuEvent(type:String,mButton:MenuButton):void
			{
				if(!mButton.isDropDownMenu)return;
				
				var dye:DynamicEvent = new DynamicEvent(ROLL_MENU_LEVEL);
				dye.rolltype = type;
				dye.text = mButton.text;
				dye.menutype = mButton.menutype;
				dye.x = mButton.x;
				dye.y = mButton.y;
				dye.width = mButton.width;
				dye.menubuttonwidth = mButton.width;
				
				this.dispatchEvent(dye);
			}
			
			/**
			 * 标识指定｛text｝一级菜单，的二级菜单当前状态是否可见
			 * 
			 * @param text:String 一级菜单名称
			 * @param mark:Boolean 状态可见标识：true可见，false不可见
			 */
			public function hasShownToMenu(text:String, mark:Boolean):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.hasShownToMenu = mark;
			}
			
			/**
			 * 添加菜单对象(MenuButton)样式
			 */
			public function addedBgStyle(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.addedBgStyle();
			}
			
			public function stopMenuTimer(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.addedBgStyle();
			}
			
			/**
			 * 删除菜单对象(MenuButton)样式
			 */
			public function removeBgStyle(text:String):void
			{
				var mButton:MenuButton = getMenuButtonByText(text);
				mButton.removeBgStyle();
			}
			
			/**
			 * 根据菜单显示名称，取得菜单对象(MenuButton)
			 */
			public function getMenuButtonByText(text:String):MenuButton
			{
				var returnvalue:MenuButton;
				for(var i:int=0, num:int=hgroup.numElements; i<num; i++)
				{
					if(hgroup.getElementAt(i) is MenuButton)
					{
						var mButton:MenuButton = hgroup.getElementAt(i) as MenuButton;
						if(mButton.text == text)
						{
							returnvalue = mButton;
							break;
						}
					}
				}
				
				return returnvalue;
			}
		]]>
	</fx:Script>
	
	<s:HGroup width="100%" gap="0" id="hgroup">
		<components:MenuButton text="请假审批流程" texttype="title" width="225" isBgEvent="false"/>
		<components:Separator/>
		<components:MenuButton menutype="{NODE}" text="节点" textcolor="0xDA2323" isDropDownMenu="true" triangleOver="true"/>
		<components:Separator/>
		<components:MenuButton menutype="{SAVE}" text="立即保存"/>
		<components:Separator/>
		<components:MenuButton menutype="{CANCEL}" text="舍弃"/>
		<components:Separator/>
		<components:MenuButton menutype="{VERIFICATION}" text="验证"/>
		<components:Separator/>
		<components:MenuButton menutype="{ALIGNMENT}" text="对齐" isDropDownMenu="true" triangleOver="true"/>
		<components:Separator/>
		<components:MenuButton menutype="{ZOOM}" text="缩放" isDropDownMenu="true" triangleOver="true"/>
		<components:Separator/>
		<components:MenuButton menutype="{GRID}" text="网格"/>
		<components:Separator/>
		<components:MenuButton menutype="{LOG}" text="日志"/>
		<components:Separator/>
		<components:MenuButton width="100%" isBgEvent="false"/>
	</s:HGroup>
	
</s:Group>
