<?xml version="1.0" encoding="utf-8"?>
<!--
	 <p><b>auth：</b>HUBO</p>
	 <p><b>create-time：</b>2012-7-1</p>
	 <p><b>mail：</b>hubo.0508ⓐgmail.com	</p>
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:navigation="com.hubo.workflow.ui.navigation.*" 
		 initialize="initializeHandler(event)"
		 width="100%"
		 height="100%" >
	<fx:Script>
		<![CDATA[
			import com.hubo.workflow.core.TweenLite;
			import com.hubo.workflow.util.Global;
			
			import mx.events.DynamicEvent;
			import mx.events.FlexEvent;
			
			private var secondaryMenuTimer:Timer;
			
			private var currentMenuText:String;
			
			protected function initializeHandler(event:FlexEvent):void
			{
				this.initEvent();
			}
			
			/**
			 * 初始化事件监听
			 */
			private function initEvent():void
			{
				this.menu.addEventListener("rollMenu",rollMenuHandler,false,0,true);
				this.secondaryMenu.addEventListener(MouseEvent.ROLL_OUT,secondaryMenuHandler,false,0,true);
				this.secondaryMenu.addEventListener(MouseEvent.ROLL_OVER,secondaryMenuHandler,false,0,true);
			}
			
			/**
			 * 二级菜单MouseEvent.ROLL_OUT与MouseEvent.ROLL_OVER事件
			 */
			protected function secondaryMenuHandler(event:MouseEvent):void
			{
				switch(event.type)
				{
					case MouseEvent.ROLL_OUT :
						this.hideSecondaryMenu(secondaryMenu);
						this.menu.removeBgStyle(currentMenuText);
						break;
					
					case MouseEvent.ROLL_OVER :
						Global.stopTimer(secondaryMenuTimer);
						this.menu.stopMenuTimer(currentMenuText);
						break;
					
					default:
						break;
				}
			}
			
			/**
			 * 一级菜单MouseEvent.ROLL_OUT与MouseEvent.ROLL_OVER事件
			 */
			protected function rollMenuHandler(event:DynamicEvent):void
			{
				this.currentMenuText = event.text;
				
				switch(event.rolltype)
				{
					case MouseEvent.ROLL_OUT :
						this.hideSecondaryMenu(secondaryMenu);
						this.menu.hasShownToMenu(this.currentMenuText, false);
						break;
					
					case MouseEvent.ROLL_OVER :
						this.showSecondaryMenu(secondaryMenu);
						this.menu.addedBgStyle(currentMenuText);
						this.menu.hasShownToMenu(this.currentMenuText, true);
						break;
					
					default:
						break;
				}
			}
			
			/**
			 * 显示二级菜单
			 */
			private function showSecondaryMenu(sm:SecondaryMenu):void
			{
				Global.stopTimer(secondaryMenuTimer);
				sm.top = 100;
				new TweenLite(sm, .2, {top: 125});
			}
			
			/**
			 * 隐藏二级菜单
			 */
			private function hideSecondaryMenu(sm:SecondaryMenu):void
			{
				Global.stopTimer(secondaryMenuTimer);
				secondaryMenuTimer = new Timer(80, 1);
				secondaryMenuTimer.addEventListener(TimerEvent.TIMER_COMPLETE, function closeAlert(event:Event):void
				{
					new TweenLite(secondaryMenu, .3, {top:87});
					Global.stopTimer(secondaryMenuTimer);
				}, false, 0, true);
				
				secondaryMenuTimer.start();
			}
		]]>
	</fx:Script>
	
	<navigation:Head/>
	
	<navigation:SecondaryMenu id="secondaryMenu" />
	
	<navigation:Menu id="menu" />
	
	<navigation:Content/>
	
	<navigation:LogOutput/>

</s:Group>
